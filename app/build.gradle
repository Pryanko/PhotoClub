
apply plugin: 'com.android.application'

android {
    compileSdkVersion setup.compileSdk
    buildToolsVersion setup.buildTools

    defaultConfig {
        applicationId "com.photoprint.photoclub"
        minSdkVersion setup.minSdk
        vectorDrawables.useSupportLibrary = true
        buildToolsVersion setup.buildTools
        targetSdkVersion setup.compileSdk
        versionCode setup.versionCode
        versionName setup.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            def debugSubVersion = "1"
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug" + debugSubVersion

            // Ресурсы
            resValue "string", "app_name", "PhotoClub debug"

            // Константы
            buildConfigField('String', 'API_BASE_URL', '"http://photo.it33.ru/api/"')
            buildConfigField('int', 'SQLITE_STUDIO_SERVICE_PORT', '12135')
            // Плейсхолдеры
            manifestPlaceholders = [
                    defaultOrientation: "unspecified"
            ]

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-main.pro', 'proguard-rules-debug.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-main.pro', 'proguard-rules-debug.pro'
        }

        beta {
            def betaSubVersion = "1"
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta" + betaSubVersion

            // Константы
            buildConfigField('String', 'API_BASE_URL', '"http://photo.it33.ru/api/"')
            resValue "string", "app_name", "PhotoClub β"

            // Константы
            matchingFallbacks = ['release']

            // Плейсхолдеры
            manifestPlaceholders = [
                    defaultOrientation: "portrait"
            ]

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-main.pro', 'proguard-rules-debug.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-main.pro', 'proguard-rules-debug.pro'
        }

        release {

            // Ресурсы
            resValue "string", "app_name", "PhotoClub"

            // Константы
            buildConfigField('String', 'API_BASE_URL', '"http://photo.it33.ru/api/"')

            // Плейсхолдеры
            manifestPlaceholders = [
                    defaultOrientation: "portrait"
            ]

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "PhotoClub_${variant.buildType.name}_${variant.versionName}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // Logger
    implementation project(':logger')
    // Network
    implementation project(':network')
    // Local DataBase
    implementation project(':localdb')
    // Utils
    implementation project(':utils')
    // Dagger
    depsDagger()
    //Test
    depsTests()
    //MapStruct
    depsMapStruct()
    // RxJava
    depsRxJava()
    //butterknife
    implementation "com.jakewharton:butterknife:${versions.butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${versions.butterknife}"
    //Default
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:recyclerview-v7:${versions.support}"
    implementation "com.android.support:design:${versions.support}"
    implementation "com.android.support:appcompat-v7:${versions.support}"
    implementation files('libsDebug/SQLiteStudioRemote.jar')
}
